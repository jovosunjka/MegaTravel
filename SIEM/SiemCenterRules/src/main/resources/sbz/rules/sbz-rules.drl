package sbz.rules;

import com.bsep_sbz.SIEMCenter.model.sbz.log.Log;
import com.bsep_sbz.SIEMCenter.model.sbz.enums.log.LogLevel;
import com.bsep_sbz.SIEMCenter.model.sbz.enums.log.LogCategory;
import com.bsep_sbz.SIEMCenter.model.sbz.enums.log.AlarmProducerType;
import com.bsep_sbz.SIEMCenter.model.sbz.log.Alarm;
import java.lang.Number;
import java.util.HashSet;


query "Get all alarms"
    $a: Alarm()
end

// Neuspešni pokušaji prijave na sistem na istoj mašini. Prijava može biti na nivou
// operativnog sistema ili na nivou simuliranog informacionog sistema
// mradovic: Put all logs in alarm ???
// WARNING: This rule is executed for every log with given settings ! Needs to be executed only once?
rule "Neuspesni pokusaji prijave na sistem na istoj masini 2+"
    when
        exists (
            Log($id: id, type == LogLevel.WARN, category == LogCategory.LOGIN, $ha: hostAddress, message.contains("login_successful:false")) and
            Number(intValue >= 2) from accumulate(
                            $l: Log(
                                //id != $id,
                                type == LogLevel.WARN,
                                category == LogCategory.LOGIN,
                                hostAddress == $ha,
                                message.contains("login_successful:false")
                            ),
                            count($l)
                        )
        )
    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.LOGIN);
        //alarm.getLogs().add($log);
        alarm.setMessage("Neuspesni pokusaji prijave na sistem na istoj masini 2+");
        insert(alarm);
end

// Neuspešni pokušaji prijave na sistem sa istim korisničkim imenom. Prijava može biti na nivou
// operativnog sistema na ili nivou simuliranog informacionog sistema
rule "Neuspesni pokusaji prijave sa istim username-om 2+"
    when
        exists (
            Log(type == LogLevel.WARN, category == LogCategory.LOGIN, message.contains("login_successful:false"), $username: getUsername() != null) and
            Number(intValue >= 2) from accumulate(
                            $l: Log(
                                type == LogLevel.WARN,
                                category == LogCategory.LOGIN,
                                getUsername() == $username,
                                message.contains("login_successful:false")
                            ),
                            count($l)
                        )
        )
    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.LOGIN);
        //alarm.getLogs().add($log);
        alarm.setMessage("Neuspesni pokusaji prijave sa istim username-om 2+");
        insert(alarm);
end

// Pokušaj prijave na nalog koji nije bio aktivan 90 ili više dana
// mradovic: WARNING: Po ovoj implementaciji login log od pre 90 dana bi trebao biti u radnoj memoriji !!!
rule "Pokusaj prijave na nalog koji nije bio aktivan 90+ dana"
    when
        $l: Log(category == LogCategory.LOGIN, $s: source, $ts: timestamp) // source je u ovom slucaju username
        Number(Log.getDaysOfInactivity(longValue, $ts.getTime()) >= 90) from accumulate(
                                    Log(
                                        category == LogCategory.LOGIN,
                                        source == $s,
                                        timestamp != $ts,
                                        $ts2: timestamp //,
                                        //message.contains("login_successfull:false")
                                    ),
                                    max($ts2.getTime())
                                )
    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.LOGIN);
        alarm.getLogs().add($l);
        alarm.setMessage("Pokusaj prijave na nalog koji nije bio aktivan 90+ dana");
        insert(alarm);
end

//Pojava loga u kome antivirsu registruje pretnju, a da u roku od 1h se ne generise log o uspešnom elimisanju pretnje
rule "Pojava loga u kome antivirus registruje pretnju, a da u roku od 1h se ne generise log o uspesnom eliminisanju pretnje"
    when
            $al1: Log($id: id, type == LogLevel.WARN, category == LogCategory.ANTIVIRUS, $ts: timestamp)
            $al2: Log(type == LogLevel.INFO, category == LogCategory.ANTIVIRUS, message.contains("solvedLogId:"+$id), diffrenceInHours($al1.timestamp) <= 1 )

    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.APP);
        alarm.getLogs().add($al1);
        alarm.getLogs().add($al2);
        alarm.setMessage("Pojava loga u kome antivirus registruje pretnju, a da u roku od 1h se ne generise log o uspesnom eliminisanju pretnje");
        insert(alarm);
end


// NOVA PRAVILA - JOVO


//15 ili više neuspešnih pokušaja prijave na različite delove informacionog sistema sa iste IP adrese u roku od 5 dana;
rule "Neuspesni pokusaji prijave 15+ u roku od 5 dana"
    when
        exists (
            Log($ipAddress: source, type == LogLevel.WARN, category == LogCategory.LOGIN, message.contains("login_successful:false")) and
            Number(intValue >= 15) from accumulate(
                            $l: Log(
                                //id != $id,
                                type == LogLevel.WARN,
                                category == LogCategory.LOGIN,
                                source == $ipAddress,
                                message.contains("login_successful:false")
                            )
                            over window: time(5d),
                            count($l)
                        )
         )
    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.LOGIN);
        //alarm.getLogs().add($log);
        alarm.setMessage("Neuspesni pokusaji prijave 15+ u roku od 5 dana");
        insert(alarm);
end


//Prijavljivanje na sistem od istog korisnika na dva ili više dela informacionog sistema
//u razmaku manjem od 10 sekundi sa različitih IP adresa
rule "Prijavljivanje na sistem u razmaku manjem od 10 sekundi sa razlicitih IP adresa"
    when
        exists (
            Log($ipAddress: source, type == LogLevel.INFO, category == LogCategory.LOGIN, message.contains("login_successful:true"), $username: getUsername() != null) and
            HashSet(size >= 2) from accumulate(
                            Log(
                                type == LogLevel.INFO,
                                category == LogCategory.LOGIN,
                                getUsername() == $username,
                                source != $ipAddress,
                                message.contains("login_successful:true"),
                                $ha: hostAddress
                            )
                            over window: time(10s),
                            init ( HashSet uniqueHostAddresses = new HashSet<String>(); ),
                            action ( uniqueHostAddresses.add($ha); ),
                            reverse ( uniqueHostAddresses.remove($ha); ),
                            result ( uniqueHostAddresses )
                        )
         )
    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.LOGIN);
        //alarm.getLogs().add($log);
        alarm.setMessage("Prijavljivanje na sistem u razmaku manjem od 10 sekundi sa razlicitih IP adresa");
        insert(alarm);
end


//U periodu od 10 dana registrovano 7 ili više pretnji od strane antivirusa za isti računar
rule "U periodu od 10 dana registrovano 7 ili vise pretnji od strane antivirusa za isti racunar"
    when
        exists (
            Log(type == LogLevel.ERROR, category == LogCategory.ANTIVIRUS, $ha: hostAddress) and
            Number(intValue >= 7) from accumulate(
                            $l: Log(
                                //id != $id,
                                type == LogLevel.ERROR,
                                category == LogCategory.ANTIVIRUS,
                                hostAddress == $ha
                            )
                            over window: time(10d),
                            count($l)
                        )
         )
    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.APP);
        //alarm.getLogs().add($log);
        alarm.setMessage("U periodu od 10 dana registrovano 7 ili vise pretnji od strane antivirusa za isti racunar");
        insert(alarm);
end

//Uspešna prijava na sistem praćena sa izmenom korisničkih podataka ukoliko je sa iste IP adrese
//u poslednjih 90 sekundi bilo registrovano 5 ili više neuspešnih pokušaja prijavljivanja na različite naloge
rule "Uspesna prijava na sistem pracena sa izmenom korisnickih podataka ukoliko je sa iste IP adrese"
    when
        exists (
            // vremenski razmak izmedju odnos uspesnog prijavljivanja sa izmenom podataka i neuspesnih 5 i vise prijavljivanja ???
            Log($ipAddress: source, type == LogLevel.INFO, category == LogCategory.LOGIN, message.contains("login_successful:true")) and
            Log(source == $ipAddress, type == LogLevel.INFO, category == LogCategory.APP, message.contains("user_data_updated_successful:true")) and

            $log: Log(source == $ipAddress, type == LogLevel.WARN, category == LogCategory.LOGIN, message.contains("login_successful:false"), $username: getUsername() != null) and
            Number(intValue >= 5) from accumulate(
                            $l: Log(
                                type == LogLevel.WARN,
                                category == LogCategory.LOGIN,
                                source == $ipAddress,
                                message.contains("login_successful:false"),
                                this == $log || getUsername() != $username // mora uracunati i ovaj log iznad
                            )
                            over window: time(90s),
                            count($l)
                        )
         )
    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.LOGIN);
        //alarm.getLogs().add($log);
        alarm.setMessage("Uspesna prijava na sistem pracena sa izmenom korisnickih podataka ukoliko je sa iste IP adrese");
        insert(alarm);
end

//Zahtevi bilo kog tipa aktiviraju alarm za DoS napad
rule "Zahtevi bilo kog tipa aktiviraju alarm za DoS napad"
    when
        Number(intValue > 50) from accumulate(
                        $l: Log() over window: time(60s),
                        count($l)
                    )
    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.APP);
        //alarm.getLogs().add($log);
        alarm.setMessage("Zahtevi bilo kog tipa aktiviraju alarm za DoS napad");
        insert(alarm);
end

//Zahtevi bilo kog tipa aktiviraju alarm za DoS napad
rule "Zahtev koji su povezani sa prijavom korisnika aktiviraju brute-force alarm"
    when
        Number(intValue > 50) from accumulate(
                        $l: Log(category == LogCategory.LOGIN) over window: time(60s),
                        count($l)
                    )
    then
        Alarm alarm = new Alarm();
        alarm.setAlarmProducerType(AlarmProducerType.APP);
        //alarm.getLogs().add($log);
        alarm.setMessage("Zahtev koji su povezani sa prijavom korisnika aktiviraju brute-force alarm");
        insert(alarm);
end